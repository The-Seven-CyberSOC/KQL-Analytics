# KQL Queries

## Web visited by user

``` kql
DeviceNetworkEvents
| where RemoteUrl has "url.com" // Replace with URL
| project TimeGenerated, InitiatingProcessAccountUpn, DeviceName, InitiatingProcessFileName, RemoteUrl, RemoteIP
```

### Grouped by visit count

``` kql
DeviceNetworkEvents
| where RemoteUrl has "url.com" // Replace with URL
| extend EventTime = TimeGenerated
| join kind=inner (
    DeviceLogonEvents
    | project DeviceId, AccountName, LogonTime = TimeGenerated
) on DeviceId
| where abs(datetime_diff("minute", EventTime, LogonTime)) < 30
| summarize VisitCount = count() by AccountName, bin(EventTime, 1d)
| sort by EventTime asc, VisitCount desc
| render columnchart with(title="Visitas Web Por Usuario", xtitle="Date", ytitle="Visit Count")
```

## PIM escalation motives

``` kql
AuditLogs
| where TimeGenerated > ago(10d) // Replace "ago(10d)" with how many days before to look up
| where OperationName == "Add member to role requested (PIM activation)"
| extend Role = tostring(TargetResources[0].displayName)
| extend TicketNumber = tostring(AdditionalDetails[7].value)
| extend User = tostring(parse_json(InitiatedBy.user).userPrincipalName)
| summarize by ResultReason, User, Role, ActivityDateTime
| order by ActivityDateTime
```

## Sign-in attempts by user

``` kql
SigninLogs
| where UserPrincipalName == "username@domain.com" // Replace with username
| summarize SignInCount = count() by UserPrincipalName, AuthenticationRequirement, TimeGenerated, AppDisplayName,  ResultSignature, ResultDescription
| order by SignInCount desc
```
## USB Connected Devices

``` kql
DeviceFileEvents
| where InitiatingProcessAccountName != "system"
| where ActionType == "FileCreated"
| where FolderPath !startswith "C:\\"
| where FolderPath !startswith "\\"
| project
    ReportId,
    DeviceId,
    InitiatingProcessAccountDomain,
    InitiatingProcessAccountName,
    InitiatingProcessAccountUpn,
    FileName,
    FolderPath,
    SHA256,
    TimeGenerated,
    SensitivityLabel,
    IsAzureInfoProtectionApplied
| join kind=inner (
    DeviceEvents
    | where ActionType == "UsbDriveMounted"
    | extend ParsedFields = parse_json(AdditionalFields)
    | extend SerialNumber = tostring(ParsedFields.SerialNumber),
             Manufacturer = tostring(ParsedFields.Manufacturer),
             ProductName = tostring(ParsedFields.ProductName),
             DriveLetter = tostring(ParsedFields.DriveLetter)
    | project
        DeviceId,
        DeviceName,
        DriveLetter,
        MountTime = TimeGenerated,
        SerialNumber,
        Manufacturer,
        ProductName
) on DeviceId
| where FolderPath startswith DriveLetter
| where TimeGenerated >= MountTime
| summarize LatestMountTime = arg_max(MountTime, SerialNumber, Manufacturer, ProductName, DeviceName)
    by ReportId,
       InitiatingProcessAccountName,
       InitiatingProcessAccountDomain
| extend Time = format_datetime(LatestMountTime, "yyyy-MM-dd")
| project
    Time,
    InitiatingProcessAccountName,
    InitiatingProcessAccountDomain,
    DeviceName,
    SerialNumber,
    Manufacturer,
    ProductName,
    LatestMountTime
| summarize count() by
    Time,
    InitiatingProcessAccountName,
    InitiatingProcessAccountDomain,
    DeviceName,
    SerialNumber,
    Manufacturer,
    ProductName
```

## Installed software 

```kql
DeviceTvmSoftwareInventory
| where SoftwareName == "program.exe" // Replace with program name
| project DeviceName, OSPlatform, OSVersion, SoftwareName, SoftwareVersion, SoftwareVendor
| order by DeviceName asc
```

## USB file downloaded


```kql
let UsbDriveMount = DeviceEvents
| where ActionType=="UsbDriveMounted"
| extend ParsedFields=parse_json(AdditionalFields)
| project DeviceId, DeviceName, DriveLetter=ParsedFields.DriveLetter, MountTime=TimeGenerated,
ProductName=ParsedFields.ProductName,SerialNumber=ParsedFields.SerialNumber,Manufacturer=ParsedFields.Manufacturer
| order by DeviceId asc, MountTime desc;
let FileCreation = DeviceFileEvents
| where InitiatingProcessAccountName != "system"
| where ActionType == "FileCreated"
| where FolderPath !startswith "C:\\"
| where FolderPath !startswith "\\"
| project ReportId,DeviceId,InitiatingProcessAccountDomain,
InitiatingProcessAccountName,InitiatingProcessAccountUpn,
FileName, FolderPath, SHA256, TimeGenerated, SensitivityLabel, IsAzureInfoProtectionApplied
| order by DeviceId asc, TimeGenerated desc;
FileCreation | lookup kind=inner (UsbDriveMount) on DeviceId
| where FolderPath startswith DriveLetter
| where TimeGenerated >= MountTime
| partition hint.strategy=native by ReportId ( top 1 by MountTime )
| order by DeviceId asc, TimeGenerated desc
| extend HostName = iff(DeviceName has '.', substring(DeviceName, 0, indexof(DeviceName, '.')), DeviceName)
| extend DnsDomain = iff(DeviceName has '.', substring(DeviceName, indexof(DeviceName, '.') + 1), "")
| extend FileHashAlgorithm = 'SHA256'
```

## Navigation history by username

```kql
DeviceNetworkEvents
| where TimeGenerated between (datetime(2025-07-03 00:00:00) .. datetime(2025-07-03 23:59:59)) // Replace date range
| where InitiatingProcessAccountName == "username"  // Replace with username
| where RemoteUrl != "" and isnotempty(RemoteUrl)
| project TimeGenerated, DeviceName, InitiatingProcessAccountName, RemoteUrl, InitiatingProcessFileName
| order by TimeGenerated asc
```

## Sharepoint Activity

### Folders

```kql
let threshold = 500;
OfficeActivity
| where EventSource == "SharePoint" 
    and OfficeWorkload has_any("SharePoint", "OneDrive") 
    and Operation has_any ("FileDownloaded", "FileSyncDownloadedFull", "FileSyncUploadedFull", "FileUploaded")
| summarize 
    count_distinct_SourceRelativeUrl=dcount(SourceRelativeUrl), 
    dirlist=make_set(SourceRelativeUrl, 10000) by UserId,ClientIP,
    UserAgent,bin(TimeGenerated, 15m)
| where count_distinct_SourceRelativeUrl >= threshold
| extend DirSample = iff(array_length(dirlist) == 1, tostring(dirlist[0]), strcat("SeeDirListField","_", tostring(hash(tostring(dirlist)))))
| extend 
    AccountName = tostring(split(UserId, "@")[0]), 
    AccountUPNSuffix = tostring(split(UserId, "@")[1])
| where UserId == "username@domain.com" // Replace with username
```

### Files

```kql
let threshold = 500;
OfficeActivity
| where EventSource == "SharePoint"
    and OfficeWorkload has_any("SharePoint", "OneDrive")
    and Operation has_any ("FileDownloaded", "FileSyncDownloadedFull", "FileSyncUploadedFull", "FileUploaded")
| summarize
    count_distinct_OfficeObjectId=dcount(OfficeObjectId),
    fileslist=make_set(OfficeObjectId, 10000)
    by UserId, ClientIP, bin(TimeGenerated, 15m)
| where count_distinct_OfficeObjectId >= threshold
| extend FileSample = iff(array_length(fileslist) == 1, tostring(fileslist[0]), strcat("SeeFilesListField", "_", tostring(hash(tostring(fileslist)))))
| extend
    AccountName = tostring(split(UserId, "@")[0]),
    AccountUPNSuffix = tostring(split(UserId, "@")[1])
| where UserId == "username@domain.com" // Replace with username
```
### Deleted Files

```kql
let threshold = 500;
OfficeActivity
| where EventSource == "SharePoint"
    and Operation has "FileDeleted"
| summarize
    count_distinct_OfficeObjectId=dcount(OfficeObjectId),
    fileslist=make_set(OfficeObjectId, 10000)
    by UserId, ClientIP, bin(TimeGenerated, 15m)
| where count_distinct_OfficeObjectId >= threshold
| extend FileSample = iff(array_length(fileslist) == 1, tostring(fileslist[0]), strcat("SeeFilesListField", "_", tostring(hash(tostring(fileslist)))))
| extend
    AccountName = tostring(split(UserId, "@")[0]),
    AccountUPNSuffix = tostring(split(UserId, "@")[1])
| where UserId == "username@domain.com" // Replace with username
```

## User Password change

```kql
AuditLogs
| where OperationName has "Change user password"
| extend TargetUsername = tostring(TargetResources[0].userPrincipalName)
| where TargetUsername has "username@domain.com" // Replace with username

```

## Password change followed by bruteforce alert

```kql
let PasswordChangeUsers = AuditLogs
| where TimeGenerated > ago(1d)
| where OperationName has "Change user password"
| extend UserPrincipalName = tostring(TargetResources[0].userPrincipalName)
| summarize by UserPrincipalName;
let authenticationWindow = 20m;
let sensitivity = 3;
SigninLogs
| where AppDisplayName =~ "Windows Sign In"
| extend FailureOrSuccess = iff(ResultType in ("0","50125","50140","70043","70044"), "Success", "Failure")
| summarize FailureCount = countif(FailureOrSuccess == "Failure"),
          SuccessCount = countif(FailureOrSuccess == "Success"),
          IPAddresses  = make_set(IPAddress, 1000)
          by bin(TimeGenerated, authenticationWindow), UserDisplayName, UserPrincipalName
| extend FailureSuccessDiff = FailureCount - SuccessCount
| where FailureSuccessDiff > 0
| summarize Diff = make_list(FailureSuccessDiff, 10000),
          TimeStamp = make_list(TimeGenerated, 10000)
          by UserDisplayName, UserPrincipalName
| extend (Anomalies, Score, Baseline) = series_decompose_anomalies(Diff, sensitivity, -1, 'linefit')
| mv-expand Diff to typeof(double), TimeStamp to typeof(datetime),
            Anomalies to typeof(double), Score to typeof(double), Baseline to typeof(long)
| where Anomalies > 0
| summarize by UserDisplayName, UserPrincipalName, Anomalies, Score, Baseline,
             FailureToSuccessDiff = Diff
| join kind=leftouter (
    SigninLogs
    | where AppDisplayName =~ "Windows Sign In"
    | extend OS           = DeviceDetail.operatingSystem,
             Browser      = DeviceDetail.browser,
             StatusCode   = tostring(Status.errorCode),
             StatusDetails= tostring(Status.additionalDetails),
             State        = tostring(LocationDetails.state),
             City         = tostring(LocationDetails.city)
    | summarize StartTime  = min(TimeGenerated),
               EndTime    = max(TimeGenerated),
               IPAddresses= make_set(IPAddress, 100),
               OS         = make_set(OS, 20),
               Browser    = make_set(Browser, 20),
               City       = make_set(City, 100),
               ResultType = make_set(ResultType, 100)
               by UserDisplayName, UserPrincipalName, UserId, AppDisplayName
) on UserDisplayName, UserPrincipalName
| project-away UserDisplayName1, UserPrincipalName1
| extend IPAddressFirst = tostring(IPAddresses[0])
| extend Name      = tostring(split(UserPrincipalName,'@',0)[0]),
         UPNSuffix = tostring(split(UserPrincipalName,'@',1)[0])
| where UserPrincipalName !in (PasswordChangeUsers)
```

## Access registers and processes created for a host to identify any non-standard file transfer

```kql
let alertTime = now();
let targetDevice = "arolvd0mj05p678.lomanegra.root";
let timeWindow = 48h;
let processEvents = DeviceProcessEvents
| where Timestamp between (alertTime - timeWindow .. alertTime + timeWindow)
| where DeviceName has targetDevice
| where InitiatingProcessFileName in~ (
    "7z.exe", "rar.exe", "zip.exe", "tar.exe", "makecab.exe",
    "ftp.exe", "scp.exe", "sftp.exe", "bitsadmin.exe", "curl.exe", "wget.exe"
) or (InitiatingProcessFileName =~ "powershell.exe" and ProcessCommandLine has_any ("Invoke-WebRequest", "Start-BitsTransfer", "Copy-Item -ToSession"))
| project
    Timestamp,
    DeviceName,
    EventType = "ProcessCreated",
    InitiatingProcess = InitiatingProcessFileName,
    Process = FileName,
    CommandLine = ProcessCommandLine,
    Details = "A potential transfer/compresion tool was executed"
| sort by Timestamp asc;
let fileEvents = DeviceFileEvents
| where Timestamp between (alertTime - timeWindow .. alertTime + timeWindow)
| where DeviceName has targetDevice
| where ActionType == "FileCreated" and (
    FolderPath endswith ".zip" or
    FolderPath endswith ".rar" or
    FolderPath endswith ".7z" or
    FolderPath endswith ".tar" or
    FolderPath endswith ".gz" or
    FolderPath endswith ".tgz"
)
| project
    Timestamp,
    DeviceName,
    EventType = "FileCreated",
    InitiatingProcess = InitiatingProcessFileName,
    Process = "N/A",
    CommandLine = "N/A",
    Details = strcat("Se creó el archivo comprimido: ", FileName)
| sort by Timestamp asc;
union processEvents, fileEvents
| sort by Timestamp asc
```

## Summarizes connections made by USBs

```kql
DeviceEvents
| where ActionType == "UsbDriveMounted"
| extend ParsedFields = parse_json(AdditionalFields)
| extend SerialNumber = tostring(ParsedFields.SerialNumber),
           Manufacturer = tostring(ParsedFields.Manufacturer),
           ProductName = tostring(ParsedFields.ProductName)
| where isnotempty(SerialNumber) and SerialNumber != "null"
| summarize FirstSeen=min(TimeGenerated),
            LastSeen=max(TimeGenerated),
            ConnectionCount=count(),
            Hostnames=make_set(DeviceName, 5),
            Usernames=make_set(InitiatingProcessAccountName, 5)
            by SerialNumber, Manufacturer, ProductName
| order by LastSeen desc
```

## Created Files from a USB

```kql
let UsbMounts = DeviceEvents
| where ActionType == "UsbDriveMounted"
| extend ParsedFields = parse_json(AdditionalFields)
| project MountTime = TimeGenerated, DeviceId, DriveLetter = tostring(ParsedFields.DriveLetter), SerialNumber = tostring(ParsedFields.SerialNumber), ProductName = tostring(ParsedFields.ProductName);
DeviceFileEvents
| where ActionType == "FileCreated"
| where FolderPath !startswith "C:\\" and FolderPath !startswith "\\"
| join kind=inner UsbMounts on DeviceId
| where FolderPath startswith DriveLetter and TimeGenerated > MountTime
| summarize arg_max(MountTime, *) by ReportId // Asocia cada archivo con la sesión de montaje más reciente
| project
    TimeGenerated,
    DeviceName,
    InitiatingProcessAccountName,
    FileName,
    FolderPath,
    SHA256,
    ProductName,
    SerialNumber
| order by TimeGenerated desc
```

## Initiated processes from a USB

```kql
let UsbMounts = DeviceEvents
| where ActionType == "UsbDriveMounted"
| extend ParsedFields = parse_json(AdditionalFields)
| project MountTime = TimeGenerated, DeviceId, DriveLetter = tostring(ParsedFields.DriveLetter), SerialNumber = tostring(ParsedFields.SerialNumber), ProductName = tostring(ParsedFields.ProductName);
DeviceProcessEvents
| where isnotempty(FolderPath)
| where FolderPath !startswith "C:\\" and FolderPath !startswith "C:"
| join kind=inner UsbMounts on DeviceId
| where FolderPath startswith DriveLetter and TimeGenerated > MountTime
| summarize arg_max(MountTime, *) by ReportId
| project
    TimeGenerated,
    Hostname = DeviceName, 
    Username = InitiatingProcessAccountName, 
    FileName,
    ProcessCommandLine,
    FolderPath,
    SHA256,
    ProductName,
    SerialNumber
| order by TimeGenerated desc
```
